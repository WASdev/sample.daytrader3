apply plugin: 'ear'
apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'maven-publish'

// Use the Liberty Gradle plugin to run our functional tests
apply plugin: 'liberty'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.4'
    }
}
archivesBaseName = 'daytrader3-ee6'

ear {
   manifest {
      attributes 'Implementation-Title': 'DayTrader 3'
   }
   archiveName="$baseName.$extension"
}

task copyDerbyLib(type: Copy) {
    into "../daytrader3-ee6-wlpcfg/shared/resources/Daytrader3_SampleDerbyLibs"
    from configurations.runtime
}

/* publishing {
    publications {
        mavenWar(MavenPublication) {
            version '1.0-SNAPSHOT'
            groupId 'net.wasdev.wlp.sample'
            artifactId 'daytrader3-ee6'

            from components.web
        }
    }
} */

// Add a functional test task that runs during the 'check' phase that uses the Liberty Gradle plugin
// to launch Liberty and run the test classes in the fvt source set
task fvt(type: Test, dependsOn: installApps) {
    group 'Verification'
    description 'Runs the functional verification tests.'
    reports.html.destination = file("$buildDir/reports/fvt")
    reports.junitXml.destination = file("$buildDir/test-results/fvt")
    include '**/fvt/**'
    exclude '**/unit/**'
    systemProperties = ['liberty.test.port': getHttpPort()]
    dependsOn libertyStart
    finalizedBy libertyStop
    // If libertyRoot is not set, automatically download & install Liberty runtime
}

test {
	reports.html.destination = file("$buildDir/reports/unit")
	reports.junitXml.destination = file("$buildDir/test-results/unit")
	include '**/unit/**'
	exclude '**/fvt/**'
}

repositories { mavenCentral(); }
dependencies {
   deploy project (path: ':daytrader3-ee6-ejb', configuration: 'archives')
   deploy project (path: ':daytrader3-ee6-web', configuration: 'archives')
   deploy project (path: ':daytrader3-ee6-rest', configuration: 'archives')
   compile group: 'org.apache.derby', name: 'derby', version: '10.10.1.1'
}
String getHttpPort() {
    def DEFAULT_HTTP_PORT = 9083
    def serverXmlFile = file(new File('../daytrader3-ee6-wlpcfg/servers/daytrader3_Sample/server.xml'))
    def portPattern = ~/.*httpPort="(\d+)".*/
    serverXmlFile.eachLine { line ->
        def portMatcher = line =~ portPattern
        if (portMatcher) {
            return portMatcher.group(1)
        }
    }
    return DEFAULT_HTTP_PORT
}

installApps.dependsOn 'ear'
libertyStart.mustRunAfter 'installApps'

liberty {
    installDir = owner.hasProperty('libertyRoot') ? "${libertyRoot}" : null
    server{
        name = 'daytrader3_Sample'
        userDir = rootProject.getProjectDir().getAbsolutePath()+'/daytrader3-ee6-wlpcfg'
        looseApplication = false
        apps = [file(ear.archivePath)]
        features {
           name = ['daytrader3_Sample']
           acceptLicense = true
        }
    }
}

if (!hasProperty('libertyRoot')) {
    fvt.dependsOn('installFeature')
}

assemble.dependsOn 'installApps','copyDerbyLib'

// Run FVT by default
check.dependsOn 'fvt'
// do not create server since the usr directory is prebuilt.\
libertyCreate.enabled = false
